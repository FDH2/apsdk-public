plugins {
    id 'maven-publish'
    id 'com.android.library'
}

ext {
    VersionNumberString = "$System.env.NumberVersion"
    if (VersionNumberString == "null" || VersionNumberString == "") {
        VersionNumberString = "0.0.0.1"
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName VersionNumberString
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -fexceptions"
                arguments "-DANDROID_STL=c++_static", "-DANDROID_TOOLCHAIN=clang"
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable = true
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }
    productFlavors {
    }
    buildToolsVersion '28.0.3'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.virtable.airplay'
                artifactId = 'apssdk'
                version = '1.0'
            }

            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.virtable.airplay'
                artifactId = 'apssdk-debug'
                version = '1.0'
            }
        }
    }
}